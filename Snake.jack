class Snake {
    field int x, y, descendants;
    field int size;
    field Link child;

    constructor Snake new(int Ax, int Ay) {
        let x = Ax;
        let y = Ay;
        let descendants = 0;
        let size = 10;
        do draw();

        return this;
    }

    method void dispose() {
        if (descendants > 0) {
            do child.dispose();
        }
        do Memory.deAlloc(this);
        return;
    }

    method int getX() {
        return x;
    }

    method int getY() {
        return y;
    }

    method void addLink() {
        if (descendants = 0) {
            let child = Link.new(x, y, 0);
            let descendants = descendants + 1;
            return;
        }
        do child.addLink();
        let descendants = descendants + 1;
        return;
    }

    method void drawSize() {
        do Screen.drawRectangle(x, y, x + size, y + size);
        return;
    }

    method void draw() {
        do Screen.setColor(true);
        do drawSize();
        return;
    }

    method void erase() {
        do Screen.setColor(false);
        do drawSize();
        return;
    }

    method void setPosition(int argX, int argY) {
        var int prevX, prevY;

        let prevX = x;
        let prevY = y;

        do erase();
        let x = argX;
        let y = argY;
        do draw();

        if (descendants > 0) {
            do child.setPosition(prevX, prevY);
        }

        return;
    }

    method void moveUp() {
        if (y > 21) {
            do setPosition(x, y - size);
        }
        return;
    }

    method void moveRight() {
        if (x < 500) {
            do setPosition(x + size, y);
        }
        return;
    }

    method void moveDown() {
        if (y < 240) {
            do setPosition(x, y + size);
        }
        return;
    }

    method void moveLeft() {
        if (x > 21) {
            do setPosition(x - size, y);
        }
        return;
    }
}
